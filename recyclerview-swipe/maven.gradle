apply plugin: rootProject.ext.plugin.maven
apply plugin: rootProject.ext.plugin.bintray

version = "1.0.4"

def siteUrl = 'https://github.com/yanzhenjie/SwipeRecyclerView'
def gitUrl = 'git@github.com:yanzhenjie/SwipeRecyclerView.git'
group = "com.yanzhenjie"
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'SwipeRecyclerView For Android'
                description 'SwipeRecyclerView'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'yanzhenjie'
                        name 'yanzhenjie'
                        email 'smallajax@foxmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "recyclerview-swipe"
        userOrg = 'yolanda'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            gpg {
                sign = true
                passphrase = properties.getProperty("bintray.gpg.password")
            }
        }
    }
}
